/*
Gradle script for building the library. Usage:
Have a local or a remote Maven repository which hosts the dependencies (only Starling 1.7.0 in this case)
Have a file called gradle.properties in [Users]/[UserName]/.gradle with the following contents:

mavenUsername=[the username to access your Maven repo]
mavenPassword=[password for your Maven repo]
flashPlayerLocation=C:\\[path to flash player]\\flashplayer.exe

(flashPlayerLocation is only needed if you want to run tests)
(OSX only) make the gradlew file executable in the root directory

Run gradlew build     to build the .swc and run tests
Run gradlew assemble  to build the .swc
Run gradlew tasks     to list all other tasks, e.g. to make project files for Flash Builder or IntelliJ IDEA
*/
import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        mavenLocal()
        maven {
            url repoManagerURL + 'repo'
            credentials {
                username = mavenUsername
                password = mavenPassword
            }
        }
    }

    dependencies {
        classpath group: 'org.gradlefx', name: 'gradlefx', version: '1.3.1'
    }
}

apply plugin: 'gradlefx'
apply plugin: 'ideafx'
apply plugin: 'maven'

group = 'com.funkypandagame'
version = '3.24'
type = 'swc'
output = 'stardust-library'
playerVersion = '15.0'
srcDirs	= ['src/main/flex']

repositories {
    mavenLocal()
    maven {
        url repoManagerURL + 'repo'
        credentials {
            username = mavenUsername
            password = mavenPassword
        }
    }
    ivy {
        name 'Adobe Air SDK'
        artifactPattern  Os.isFamily(Os.FAMILY_WINDOWS) ?
                'http://airdownload.adobe.com/air/win/download/[revision]/[module]_Compiler.[ext]' :
                'http://airdownload.adobe.com/air/mac/download/[revision]/[module]_Compiler.[ext]'
    }
}

dependencies {
    airSDK group: 'com.adobe', name: 'AIRSDK', version: '15.0', ext: Os.isFamily(Os.FAMILY_WINDOWS) ? 'zip' : 'tbz2'

    external 'com.gamua:starling-framework:1.7.0'
}

additionalCompilerOptions = [
        '-inline',
        // AS3 metadata
        '-keep-as3-metadata+=Inline,Embed,SWF,Transient',
        '-debug=false',
        '-verbose-stacktraces=true'
]

sdkAutoInstall {
    showPrompts = false
}

///////////////////////////////////////// Wrapper
task wrapper(type: Wrapper) {
    group = 'Buildmaster'
    description = 'Generates gradlew and gradlew.bat bootstrap scripts.'
    gradleVersion = '2.6'
    // place jar file and properties into a subdirectory to avoid root dir clutter
    jarFile = 'gradle/wrapper/gradle.jar'
}

///////////////////////////////////////// Maven
// this is a workaround, see https://github.com/GradleFx/GradleFx/issues/91
uploadArchives {
    description 'Deploys the existing .swc to the local or remote Maven repository. Also creates the pom.xml file.'
    repositories {
        mavenDeployer {
            // invoke this with the -PdeployToRepoManager command line argument
            if (project.hasProperty('deployToRepoManager')) {
                repository(url: repoManagerURL + 'funkypanda') {
                    authentication(userName: mavenUsername, password: mavenPassword)
                }
            }
            else {
                repository url: mavenLocal().url
            }
            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                project.configurations["external"].allDependencies.each { Dependency dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                    dependencyNode.appendNode('type', 'swc')

                }
            }
        }
    }
}
